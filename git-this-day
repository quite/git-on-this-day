#!/usr/bin/awk -f

BEGIN {
  srand()

  if (system("git rev-parse --show-toplevel >/dev/null 2>&1")) {
    exit 1
  }

  refycmd = "git show -s --format=%cd --date=format:%Y"
  refycmd | getline ly
  "date +%Y" | getline cy
  if (ly == cy) {
    ly--;
  }

  while ("git rev-list --max-parents=0 HEAD" | getline root) {
    refycmd " " root | getline y
    if (!fy || y < fy) fy = y;
  }

  "date +%m-%d" | getline md
  for (y = fy; y <= ly; y++) {
    while (sprintf("git log --pretty=tformat:%%H " \
                   "--since=\"%s-%s 00:00\" --until=\"%s-%s 23:59\"",
                   y, md, y, md) | getline ref) {
      refs[refn++] = ref
    }
  }

  if (!refn) {
    print "No commit on this day in history"
    exit 0
  }

  exit system("git show -s " refs[int(rand() * refn)])
}
